plugins {
    id 'java'
    id 'maven-publish'
    id 'application'

    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'org.spongepowered.mixin' version '0.7.+'
    id "com.modrinth.minotaur" version "2.+"
}

def getGitRef() {
    try {
        final def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (final Throwable ignored) {
        return 'unknown'
    }
}

final String semver = "${version_major}.${version_minor}.${version_patch}"

String build_number = System.getenv('PROMOTED_NUMBER')
if (build_number == null)
    build_number = System.getenv('BUILD_NUMBER')
if (build_number == null)
    build_number = getGitRef()

version = "${semver}+${build_number}"
group = 'li.cil.sedna'

java.toolchain {
    languageVersion = JavaLanguageVersion.of(17)
    //vendor = JvmVendorSpec.ADOPTOPENJDK
}

repositories {
	maven { url "https://www.cursemaven.com" }
	mavenCentral()
    ["North-Western-Development/sedna", "North-Western-Development/minux", "fnuecke/ceres"].forEach(repo -> {
        maven {
            url = uri("https://maven.pkg.github.com/${repo}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.key") ?: System.getenv("GPR_KEY")
            }
        }
    })
}

def tryProject(final projectName, final libraryName) {
    findProject(projectName) ?: libraryName
}

dependencies {
	minecraft "net.minecraftforge:forge:1.20.1-47.4.10"
	implementation 'com.google.guava:guava:16+'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'it.unimi.dsi:fastutil:8.5.8'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.0'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.0'
    implementation 'org.ow2.asm:asm-commons:9.2'
    implementation 'org.ow2.asm:asm:9.1'

    implementation 'org.jline:jline:3.20.0'
    implementation 'org.jline:jline-terminal-jna:3.20.0'
    implementation 'net.java.dev.jna:jna:5.9.0'

    implementation tryProject(':modules:ceres', 'li.cil.ceres:ceres:0.0.4')
    implementation tryProject(':modules:sedna', 'li.cil.sedna:sedna:2.0.13')
    implementation tryProject(':modules:sedna-buildroot', 'li.cil.sedna:sedna-buildroot:0.0.64')
	implementation tryProject(':modules:oc2r', 'curse.maven:oc2r-1037738:6280699')

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url System.getenv('MAVEN_PATH')
        }
    }
}

application {
    mainClassName = 'li.cil.sedna.cli.Main'
}

run {
    standardInput = System.in
    standardOutput = System.out
}

test {
    useJUnitPlatform()
}


minecraft {
    mappings channel: "official", version: "1.20.1"

    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        configureEach {
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"

            mods {
                oc2 {
                    source sourceSets.main
                }
            }
        }

    }
}


